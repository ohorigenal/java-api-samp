plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
}

archivesBaseName = "java-api-samp"
group = 'com.compass'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.retry:spring-retry'
	// https://github.com/springdoc/springdoc-openapi
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.7'
	// https://projectlombok.org/features/all
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.springframework.boot:spring-boot-starter-aop'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.squareup.okhttp3:okhttp:4.9.3'
	testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
	testImplementation 'io.projectreactor:reactor-test:3.4.16'

}

test {
	useJUnitPlatform()
	exclude("/com/compass/javaapisamp/integration/**")
	finalizedBy jacocoTestReport // report is always generated after tests run
}

task localIntegration (type: Test) {
	useJUnitPlatform()
	systemProperty("integration.url", "http://localhost:8080")
	include("/com/compass/javaapisamp/integration/**")
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}
